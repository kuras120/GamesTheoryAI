plugins {
    id 'jacoco'
    id 'application'
    id 'io.freefair.lombok' version "$LOMBOK_PLUGIN_VERSION"
    id 'org.openjfx.javafxplugin' version "$JAVAFX_PLUGIN_VERSION"
    id "com.github.johnrengelman.shadow" version "$SHADE_PLUGIN_VERSION"
    id "org.ajoberstar.grgit" version "$GRGIT_PLUGIN_VERSION" apply false
}

import org.ajoberstar.grgit.Grgit

group 'com.games.theory'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.jfoenix:jfoenix:$JFOENIX_VERSION"
    implementation "com.google.guava:guava:$GUAVA_VERSION"
    implementation "commons-io:commons-io:$COMMONS_IO_VERSION"
    implementation "ch.qos.logback:logback-classic:$LOGBACK_VERSION"
    testImplementation "org.junit.jupiter:junit-jupiter:$JUNIT_VERSION"
}

build {
    doFirst {
        def AiDir = "$projectDir/src/main/java/com/games/theory/tictactoe/AI"
        delete AiDir
        def neuralNetwork = Grgit.clone(
                dir: AiDir,
                uri: "https://github.com/kuras120/NeuralNetworks.git"
        )
        neuralNetwork.close()
    }
}

compileJava {
    sourceCompatibility = "$JAVA_VERSION"
    targetCompatibility = "$JAVA_VERSION"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

application {
    mainClassName = 'com.games.theory.GUI'
}

javafx {
    version = "$JAVAFX_VERSION"
    modules = ['javafx.controls', 'javafx.fxml']
}

jacoco {
    toolVersion = "$JACOCO_PLUGIN_VERSION"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
    }
}
